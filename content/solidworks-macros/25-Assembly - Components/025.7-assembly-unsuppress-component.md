---
categories: Solidworks-macro
title:  Solidworks VBA Macro - Unsuppress Component
permalink: /solidworks-vba-macros/assembly-unsuppress-component/
tags:   [Solidworks Macro]
---

## Objective

In this article, we understand "how to" **Unsuppress Component** in **Assembly document** from VBA macro.

This is most updated method of **Unsuppress Component** in an assembly document.

## Results We Can Get

Below image shows the result we get.

[![assembly-unsuppress-component](assembly-unsuppress-component/final-result-gif.gif)](assembly-unsuppress-component/final-result-gif.gif)

We **Show Component** in simple manners.

There are no extra steps required.

To get the correct result, please follow the steps correctly.
{: .notice--warning}

## Macro Video

Below üé¨ video shows how to **Unsuppress Component** from *SOLIDWORKS VBA Macros*.

<iframe src="https://www.youtube.com/embed/Gem2n32rwf4" frameborder="0" allowfullscreen></iframe>
<br>

***Above video is just for visualization and there is no explanation.*** 

***I have explained every line in this article.***

It is advisable to watch video, since it helps you to better understand the process.
{: .notice--warning}

## VBA Macro

Below is the ***VBA macro*** for *Show Component*.

```vb
Option Explicit

' Variable for Solidworks Application
Dim swApp As SldWorks.SldWorks

' Variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2

' Variable for Solidworks Assembly
Dim swAssembly As SldWorks.AssemblyDoc

' Variable for Solidworks Component
Dim swComponent As SldWorks.Component2

' Program to UnSuppress Selected Component
Sub main()
  
  ' Set Solidworks Application variable to current application
  Set swApp = Application.SldWorks
  
  ' Set Solidworks document variable to currently opened document
  Set swDoc = swApp.ActiveDoc
  
  ' Check if Solidworks document is opened or not
  If swDoc Is Nothing Then
    MsgBox "Solidworks document is not opened."
    Exit Sub
  End If
  
  ' Set Solidworks Assembly document
  Set swAssembly = swDoc
  
  ' Variable for List of elements
  Dim vArray As Variant
  
  ' Get Components list in opened assembly
  vArray = swAssembly.GetComponents(False)
  
  ' Variable for component
  Dim component As Variant
  
  ' Loop Components List
  For Each component In vArray
  
    ' Set Solidworks Component variable
    Set swComponent = component
    
    ' If current component is Suppress
    If swComponent.IsSuppressed Then

      ' Select the component
      swComponent.Select False
      
      ' UnSuppress selected component
      swDoc.EditUnsuppress2
      
    End If
    
  Next component
  
End Sub
```

## Prerequisite

There are some *prerequisites* for this article.

* Knowledge of **VBA programming language** is ‚ùó***required***.
* We use existing parts in Assembly document.
* Both components are fully constraint as shown in below image.
* We select the part which we want to Show.

[![prerequisite](assembly-unsuppress-component/prerequisite.png)](assembly-unsuppress-component/prerequisite.png)

We will apply checks in this article, so the code we write, should be **error free** mostly.
{: .notice}

## Steps To Follow

This **VBA macro** can be divided into following sections:

1. *Create Global Variables*
2. *Initialize Global Variables*
3. *Unsuppress Component*

***Every section with each line is explained below.***

I also give some ***links (see icon üöÄ)*** so that you can go through them if there are anything I explained in previous articles.
{: .notice}

### Create Global Variables

In this section, we create global variables.

```vb
Option Explicit
```

* **Purpose**: Above line forces us to define every variable we are going to use. 
* **Reference**: üöÄ **[SOLIDWORKS Macros - Open new Part document](/solidworks-macros/open-new-document)** article.

```vb
' Variable for Solidworks application
Dim swApp As SldWorks.SldWorks
```

* **Purpose**: In above line, we create a variable for *Solidworks application*.
* **Variable Name**: `swApp`
* **Type**: `SldWorks.SldWorks`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISldWorks_members.html)**.

```vb
' Variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2
```

* **Purpose**: In above line, we create a variable for *Solidworks document*. 
* **Variable Name**: `swDoc` 
* **Type**: `SldWorks.ModelDoc2`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IModelDoc2_members.html)**.

```vb
' Variable for Solidworks Assembly
Dim swAssembly As SldWorks.AssemblyDoc
```

* **Purpose**: In above line, we create a variable for *Solidworks Assembly*.
* **Variable Name**: `swAssembly`
* **Type**: `SldWorks.AssemblyDoc`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IAssemblyDoc_members.html)**.

```vb
' Variable for Solidworks Component
Dim swComponent As SldWorks.Component2
```

* **Purpose**: In above line, we create a variable for *Solidworks Component*.
* **Variable Name**: `swComponent` 
* **Type**: `SldWorks.Component2`.
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IComponent2_members.html)**.


***These all are our global variables.***

They are **SOLIDWORKS API Objects**.

```vb
' Program to Unsuppress Selected Component
Sub main()

End Sub
```

* In above line, we create *Program to Unsuppress Selected Component*.
* This is a **`Sub`** procedure which has name of **`main`**. 
* This procedure hold all the *statements (instructions)* we give to computer.
* **Reference**: Detailed information üöÄ **[VBA Sub and Function Procedures](/vba/sub-and-function-procedure/)** article of this website.

### Initialize Global Variables

In this section, we initialize global variables.

```vb
' Set Solidworks Application variable to current application
Set swApp = Application.SldWorks
```

* In above line, we set *value* of **`swApp`** variable.
* This *value* is currently opened Solidworks application.

```vb
' Set Solidworks document variable to currently opened document
Set swDoc = swApp.ActiveDoc
```

* In above line, we set *value* of **`swDoc`** variable.
* This *value* is currently *opened part document*.

```vb
' Check if Solidworks document is opened or not
If swDoc Is Nothing Then
  MsgBox ("Solidworks document is not opened.")
  Exit Sub
End If
```

* In above code block, we check if we successfully set the value of **`swDoc`** variable.
* We use üöÄ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`swDoc Is Nothing`**
* When this condition is **`True`**, 
  * We show and üöÄ **[message window](/vba/msgBox-function/)** to user.
  * **Message**: *SOLIDWORKS document is not opened.*
  * Then we **stop** our macro here.

```vb
' Set Solidworks Assembly document
Set swAssembly = swDoc
```

* In above line, we set *value* of **`swAssembly`** variable.
* This *value* is **`swDoc`** variable.

### Unsuppress Component

In this section, we perform *UnSuppress Component* action.

```vb 
' Variable for List of elements
Dim vArray As Variant
```

* **Purpose**: In above line, we create a variable for *List of elements*.
* **Variable Name**: `vArray`
* **Type**: `Variant`

```vb 
' Get Components list in opened assembly
vArray = swAssembly.GetComponents(False)
```

* In above line, we set the value of **`vArray`** variable. 
* We set value by **`GetComponents`** method of **`swAssembly`** variable.
* By passing **`False`** to **`GetComponents`** method, we get all components from Feature Tree.

```vb 
' Variable for component
Dim component As Variant
```

* In above line, we create **`component`** variable for looping.
* **Variable Name**: `component`
* **Type**: `Variant`

```vb 
' Loop Components List
For Each component In vArray
  
' Move to next component
Next component
```

* In above line, we create a `For Each` loop.
* In this loop, **`component`** variable loops every item in **`vArray`**.

```vb 
' Set Solidworks Component variable
Set swComponent = component
```

* In above line, we set *value* of **`swComponent`** variable.
* This *value* is current value of array **`vArray`**.
* Current value is represented by **`component`** variable.

```vb 
' If current component is Suppress
If swComponent.IsSuppressed Then
  
End If
```

* In above code block, we check *if current component is Suppress*.
* We use üöÄ **[IF statement](/vba/vba-if-then-structure-select-case/)** for checking.
* **Condition**: **`swComponent.IsSuppressed`**

```vb 
' Select the component
swComponent.Select False
```

* In above line, we *select the component*.
* We use **`Select`** method of **`swComponent`** variable.

```vb 
' UnSuppress selected component
swDoc.EditUnsuppress2
```

* In above line, we Show selected component.
* We use **`EditUnsuppress2`** method of **`swDoc`** variable.
* This method return nothing.

- **Return Value** : This `EditUnsuppress2` method return **`True`** or **`False`**.

Now we run the macro and after running macro we show selected component as shown in below image.

[![assembly-unsuppress-component](assembly-unsuppress-component/final-result-gif.gif)](assembly-unsuppress-component/final-result-gif.gif)

**This is it !!!**

*I hope my efforts will helpful to someone!* üòä

If you found anything to **add or update**, please let me know on my *e-mail* üìß.

Hope this post helps you to **Show Component** with SOLIDWORKS VBA Macros.

For more such tutorials on **SOLIDWORKS VBA Macro**, do come to this website after sometime.

*If you like the post then please share it with your friends also.* üôèüèª

*Do let me know by you like this post or not!*

*Till then, Happy learning!!!*

- [üéÅ Download 5 Free SolidWorks Macros ‚Üí](/download-solidworks-macros/)
